# recursively search for string, feed matches to fzf with preview, launch vim with selected match

emulate -L zsh

# use bat with syntax highlight support, if it's avaialble
local preview_cmd
if (( ${+commands[bat]} )); then
    preview_cmd='bat --style=numbers --color=always --highlight-line=${2} ${1}'
else
    # replace here used to highlight line with match
    preview_cmd='sed -E "s/(.*'$*'.*)/'$bg[grey]'\1'$reset_color'/gI;" < ${1}'
fi

# prefer ag to grep
if (( ${+commands[ag]} )); then
    ag --nogroup --color --silent "$*"
else
    grep --line-number --recursive --ignore-case --color=always --no-messages "$*"
fi | fzf --ansi --layout=reverse-list --no-sort --height=50% --delimiter=: \
         --preview=${preview_cmd} \
         --preview-window='right,50%,+{2}/2' \
         --bind='enter:become(vi {1} +{2})'

# vim: ft=zsh
